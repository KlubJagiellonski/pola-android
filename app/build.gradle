import java.util.logging.Level
import java.util.logging.Logger

buildscript {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

plugins {
    id 'kotlin-android'
    id 'com.android.application'
    id 'com.google.firebase.crashlytics'
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

android {
    compileSdkVersion 31
    buildToolsVersion = "30.0.3"

    defaultConfig {
        applicationId "pl.pola_app"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 39
        versionName "1.9.8"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            File keystore = file("../keystore.properties")
            if (keystore.exists()) {
                Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(keystore))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                Logger.getLogger("pl.pola_app").log(Level.WARNING, "SIGNING: 'keystore.properties' file does not exist!")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_FIREBASE", "true"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_FIREBASE", "true"
        }
        dev {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_FIREBASE", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

def build_param = "${build}"

if (build_param != "dev") {
    //exclude dev builkd
    android.variantFilter { variant ->
        if (variant.buildType.name == 'dev') {
            variant.setIgnore(true)
        }
    }
} else {
    //exclude all except dev
    android.variantFilter { variant ->
        if (variant.buildType.name != 'dev') {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.github.jkwiecien:EasyImage:1.3.1'
    implementation 'com.facebook.device.yearclass:yearclass:2.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation('com.journeyapps:zxing-android-embedded:4.3.0') { transitive = false }
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.jakewharton:butterknife:10.2.3'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.dagger:dagger:2.41'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'org.parceler:parceler-api:1.1.13'
    implementation 'com.github.tajchert:nammu:1.2.1'
    implementation 'androidx.webkit:webkit:1.4.0'

    implementation 'com.google.firebase:firebase-core:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.9'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.firebase:firebase-bom:30.5.0'
    implementation 'com.google.firebase:firebase-messaging:23.0.6'
    implementation 'com.google.android.play:core:1.10.3'
    // Kotlin
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.41'
    annotationProcessor 'org.parceler:parceler:1.1.13'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.0'
    testImplementation 'org.robolectric:robolectric:4.7.3'
}

apply plugin: 'com.google.gms.google-services'
