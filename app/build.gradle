import java.util.logging.Level
import java.util.logging.Logger

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "pl.pola_app"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 24
        versionName "1.5.2"
    }

    signingConfigs {
        release {
            File keystore = file("../keystore.properties")
            if (keystore.exists()) {
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(keystore))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                Logger.getLogger("pl.pola_app").log(Level.WARNING, "SIGNING: 'keystore.properties' file does not exist!")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }

    apt 'com.google.dagger:dagger-compiler:2.2'
    apt 'org.parceler:parceler:1.1.1'

    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.github.jkwiecien:EasyImage:1.3.1'
    compile 'com.facebook.device.yearclass:yearclass:2.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.google.dagger:dagger:2.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'org.parceler:parceler-api:1.1.1'
    compile 'com.github.tajchert:nammu:1.1.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
}
